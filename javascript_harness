#!/usr/bin/perl -w

use strict;
use autodie;
use TAP::Harness;

# Cache the started selenium object so multiple tests
# don't have to restart saving lots of time.
our $Selenium;
sub get_selenium {
    require WWW::Selenium;
    return $Selenium if $Selenium;

    $Selenium = WWW::Selenium->new(
        host => "localhost",
        post => 4444,
        browser => "*firefox",
        browser_url => "file://nothing"
    );
    $Selenium->start;

    return $Selenium;
}


sub _testify_javascript {
    my $file = shift;

    open my $fh, "<", $file;
    my $javascript = join "", <$fh>;

    use Cwd;
    use File::Temp 'tempfile';

    my $cwd = cwd;

    my($tmpfh, $tmpfile) = tempfile( CLEANUP => 1 );
    print $tmpfh <<"END_OF_HTML";
<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML//EN">
<html>
  <head>
    <script type="text/javascript" src="$cwd/t/lib/Test/Builder.js"></script>
    <script type="text/javascript" src="$cwd/t/lib/Test/Simple.js"></script>
    <title>TAP test</title>
</head>

<body>
<pre id="TAP">
<script type="text/javascript">
$javascript
</script>
</pre>
</body>
</html>
END_OF_HTML

    return "file://$tmpfile";
}


my $harness = TAP::Harness->new({
    exec => sub {
        my( $harness, $file ) = @_;

        my($type) = $file =~ m{\.(js|html)$};
        return unless $type;  # run Perl normally

        require File::Spec;
        my $sel = get_selenium();

        my $url = $type eq 'js'   ? _testify_javascript($file) :
                  $type eq 'html' ? File::Spec->rel2abs($file) :
                                    die "Unknown type $type";

        $sel->open($url);
        return $sel->get_text(q{//pre[@id="TAP"]});
    },
});

my @files = @ARGV ? @ARGV : glob("t/*.{t,js,html}");
my $result = $harness->runtests(@files);

# Have to do this else the Firefox instance will not be stopped
# and we will dump core.
$Selenium->stop if $Selenium;

exit($result->all_passed ? 0 : 1);
